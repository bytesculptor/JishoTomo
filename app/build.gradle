apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

sourceCompatibility = 1.8

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "net.emojiparty.android.jishotomo"
    minSdkVersion 17
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  signingConfigs {
    config {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.config
    }
    debug {
      applicationIdSuffix ".debug"
      debuggable true
    }
  }
  dataBinding {
    enabled = true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  testOptions {
    animationsDisabled = true
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'androidx.appcompat:appcompat:1.0.2'
  implementation 'com.google.android.material:material:1.1.0-alpha09'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test:rules:1.2.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
  implementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'

  // ViewModel and LiveData
  implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
  annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.0.0"
  // Room
  implementation 'androidx.room:room-runtime:2.2.0-alpha01'
  annotationProcessor "androidx.room:room-compiler:2.2.0-alpha01"
  // Paging
  implementation 'androidx.paging:paging-runtime:2.1.0'
  // Test helpers for LiveData
  androidTestImplementation 'androidx.arch.core:core-testing:2.0.1'
  // Test helpers for Room
  testImplementation 'androidx.room:room-testing:2.2.0-alpha01'
  implementation 'com.github.humazed:RoomAsset:1.0.3'

  // Dagger 2
  implementation "com.google.dagger:dagger-android-support:2.13"
  implementation "com.google.dagger:dagger:2.13"
  annotationProcessor "com.google.dagger:dagger-compiler:2.13"

  // Firebase Core and Crashlytics: Analytics, Crash Reporting
  implementation 'com.google.firebase:firebase-core:17.0.1'
  implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

  // CSV export
  implementation("com.opencsv:opencsv:4.0") {
    exclude group: 'commons-logging', module: 'commons-logging'
  }
}

apply plugin: 'com.google.gms.google-services'
